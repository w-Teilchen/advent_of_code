set dotenv-load

day := `date +%d`
dayWithout0 := trim_start_match(day, "0")
year := "2025"
folder := "day_" + day

# Format, lint, and run the program for today.
run:
    #!/usr/bin/env bash
    cd {{folder}}
    cargo fmt
    cargo clippy --all-targets --all-features
    RUSTFLAGS="-Awarnings" cargo build --release
    time ./target/release/day_{{day}}.exe

bench:
    #!/usr/bin/env bash
    cd {{folder}}
    RUSTFLAGS=-Awarnings cargo build --release
    # hyperfine --warmup 5 ./target/release/day_{{day}}.exe
    hyperfine --warmup 5 'cargo run --release --quiet --bin day_{{day}}'


# Begin working on today's problem.
# Downloads input, creates template and opens the problem and code.
begin:
    #!/usr/bin/env bash
    set -euo pipefail
    shopt -s expand_aliases
    source ~/.bashrc

    cargo new {{folder}}
    cd {{folder}}
    cargo add lib --path ../lib
    cp ../template.rs "src/main.rs"
    cargo fmt
    curl --silent "https://adventofcode.com/{{year}}/day/{{dayWithout0}}/input" \
        -H "Cookie: session=$AOC_SESSION" > "input"
    touch example
    code .
    code example
    start "https://adventofcode.com/{{year}}/day/{{dayWithout0}}"

